.Lexer
    @special_symbol:  r`.\@+`
    @lexeme_name: r`@(\@ | _)+`
    @semicolon: r`:`
    @identifier: r`( \@ | _ )+`
    @regex: r` r \` ( \\ \` | [~ \`] )* \` `
    @literal: r` " ( \\" | [~"] )* " `
    @or: r`\|`
    @pos_arg: r`$ \d+`
    @comment: r`# [~\n]* \n`

    @spaces: r`\s+`
    @newlines: r`\n+`

    .Skip spaces
    .Skip comment
.End

.Parser

    .Start start

    start -> lexer_block @newlines parser_block @eof                          : add($0), add($2)

    # ========================================================
    #  Utility
    # ========================================================

    newlines -> newlines @newlines | @newlines

    lexeme_def_or -> @or | @newlines @or

    # ========================================================
    #  Lexer
    # ========================================================

    lexer_block -> ".Lexer" newlines lexer_definitions newlines ".End"    : adopt($2)

    lexer_definitions -> lexer_definitions newlines lexeme_line      : adopt($0), add($2)
        | lexeme_line

    lexeme_line -> lexeme_definition                                        : node("Lexeme"), adopt($0)
        | skip_definition                                                   : node("Skip"), add($0)

    lexeme_definition -> @lexeme_name @semicolon @regex                     : add($0), add($2)

    # ========================================================
    #  Parser
    # ========================================================

    parser_block -> ".Parser" newlines start_indicator newlines parser_definitions newlines ".End"     : add($2), add($4)

    start_indicator -> ".Start" @identifier                                                         : add($1)

    parser_definitions -> parser_definitions newlines parser_definition                            : adopt($0), add($2)
        | parser_definition

    parser_definition -> @identifier "->" production_lines                                          : push("production_name", $0), add($2)

    production_lines -> production_and_data                                                         : replace($0)
        | production_lines or production_and_data                                                   : node("options"), adopt($0), add($2)

    or -> @or

    production_and_data -> production_atoms
        | production_atoms @semicolon actions                                                       : add($0), add($2)

    production_atoms -> production_atoms production_atom                                            : adopt($0), adopt($1)
        | production_atom : replace($0)

    production_atom -> "(" production_atoms ")"     : node("grouping"), adopt($1)
        | @identifier
        | @literal
        | @lexeme_name

    actions -> actions "," action                   : adopt($0), add($2)
        | action

    action -> @identifier "(" args ")"          : add($0), add($2)

    args -> args "," arg : adopt($0), add($2)
        | arg

    arg -> @pos_arg : replace($0)
        | @literal  : replace($0)

.End